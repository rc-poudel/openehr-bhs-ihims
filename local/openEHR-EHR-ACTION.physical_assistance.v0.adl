archetype (adl_version=1.4; uid=bc89a5c0-5756-46b9-af5d-d94d2fb43608)
	openEHR-EHR-ACTION.physical_assistance.v0

concept
	[at0000]

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["date"] = <"2025-05-29">
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
	>
	lifecycle_state = <"in_development">
	other_contributors = <"AUCDI Clinical Design Group (CDG), Sparked FHIR accelerator, Australia", ...>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details about the provision of physical assistance at clinically relevant events in the intervention lifecycle, from planning to completion, and including deviations or interruptions.">
			keywords = <"intervention", ...>
			copyright = <"© Sparked AU FHIR accelerator, openEHR Foundation">
			use = <"Use to record details about the provision of physical assistance at clinically relevant events in the intervention lifecycle, from planning to completion, and including deviations or interruptions.

The scope of this data group includes a wide range of physical assistance, including but not limited to activities of daily living:
- Mobility assistance – such as walking, standing, or transferring 
- Personal activities and hygiene – such as bathing, dressing, grooming, and toileting.
- Feeding assistance
- Therapeutic exercises – such as hands-on support during physical therapy exercises or rehabilitation activities
- Use of assistive devices – such as helping with the use of mobility aids, prosthetic device, or communication devices.     

Record one instance per careflow step or event for each specified assistance activity in a health record. If the assistance consists of a single activity, the 'Assistance provided' and the 'Assistance completed' careflow step may be recorded simultaneously. However, if the assistance consists of multiple component activities, record a separate instance of the 'Assistance provided' careflow step for each component activity provided, and record the 'Assistance completed' step only after the final component activity has been provided.">
			misuse = <"Not to be used to record other types of assistance, other than physical or hands-on support. The need for a specific archetype for this purpose is acknowledged but has not yet been developed.">
		>
	>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Requirements identified by clinicians, reflecting current clinical practice and workflows.">
		["current_contact"] = <"Heather Leslie, Atomica Informatics<heather.leslie@atomicainformatics.com>">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"4B580525FE0E0F8545949D8424562752">
		["build_uid"] = <"c54ac301-35cd-4bf8-8bb2-febe3c8abff0">
		["revision"] = <"0.0.1-alpha">
	>

definition
	ACTION[at0000] matches {    -- Physical assistance
		description matches {
			ITEM_TREE[at0001] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0002] matches {    -- Assistance type
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0003] occurrences matches {0..1} matches {    -- Assistance category
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0004] occurrences matches {0..1} matches {    -- Description
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0005] occurrences matches {0..1} matches {    -- Clinical indication
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0007] occurrences matches {0..*} matches {    -- Additional details
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0008] occurrences matches {0..1} matches {    -- Scheduled date/time
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0025] occurrences matches {0..1} matches {    -- Outcome
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0009] occurrences matches {0..1} matches {    -- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0013] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0020] occurrences matches {0..1} matches {    -- Requestor order identifier
						value matches {
							DV_TEXT matches {*}
							DV_IDENTIFIER matches {*}
						}
					}
					allow_archetype CLUSTER[at0021] occurrences matches {0..*} matches {    -- Requestor
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0022] occurrences matches {0..1} matches {    -- Receiver order identifier
						value matches {
							DV_TEXT matches {*}
							DV_IDENTIFIER matches {*}
						}
					}
					allow_archetype CLUSTER[at0023] occurrences matches {0..*} matches {    -- Receiver
						include
							archetype_id/value matches {/.*/}
					}
					allow_archetype CLUSTER[at0024] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
		ism_transition matches {
			ISM_TRANSITION[at0010] matches {    -- Assistance planned
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[openehr::526]
						}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::at0010]    -- Assistance planned
						}
					}
				}
			}
			ISM_TRANSITION[at0011] matches {    -- Assistance request sent
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[openehr::526]
						}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::at0011]    -- Assistance request sent
						}
					}
				}
			}
			ISM_TRANSITION[at0012] matches {    -- Assistance postponed
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[openehr::527]
						}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::at0012]    -- Assistance postponed
						}
					}
				}
			}
			ISM_TRANSITION[at0014] matches {    -- Assistance cancelled
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[openehr::528]
						}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::at0014]    -- Assistance cancelled
						}
					}
				}
			}
			ISM_TRANSITION[at0015] matches {    -- Assistance scheduled
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[openehr::529]
						}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::at0015]    -- Assistance scheduled
						}
					}
				}
			}
			ISM_TRANSITION[at0016] matches {    -- Assistance provided
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[openehr::245]
						}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::at0016]    -- Assistance provided
						}
					}
				}
			}
			ISM_TRANSITION[at0017] matches {    -- Assistance suspended
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[openehr::530]
						}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::at0017]    -- Assistance suspended
						}
					}
				}
			}
			ISM_TRANSITION[at0018] matches {    -- Assistance abandoned
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[openehr::531]
						}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::at0018]    -- Assistance abandoned
						}
					}
				}
			}
			ISM_TRANSITION[at0019] matches {    -- Assistance completed
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[openehr::532]
						}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::at0019]    -- Assistance completed
						}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Physical assistance">
					description = <"Hands-on support provided by a caregiver to enable an individual to complete a task they cannot perform independently and/or safely due to a limitation in strength, mobility, coordination, communication, cognition, or sensory abilities, such as visual or auditory impairments.">
				>
				["at0001"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Assistance type">
					description = <"Name of the specific type of physical support or help required.">
					comment = <"For example: washing hair, toileting, transfer from wheelchair to car, or use of a communication device. Use of a clinical terminology is recommended whenever possible. Free text entry should be allowed only when an appropriate coded value is not available.">
				>
				["at0003"] = <
					text = <"Assistance category">
					description = <"An overarching grouping for the assistance identified in 'Assistance type'.">
					comment = <"For example: activity of daily living, mobilty.">
				>
				["at0004"] = <
					text = <"Description">
					description = <"Narrative description about the physical assistance, relevant to the active careflow step.">
				>
				["at0005"] = <
					text = <"Clinical indication">
					description = <"The clinical reason for the physical assistance.">
					comment = <"For example: diabetes control; or cancer treatment. Coding of the indication with a terminology is preferred, where possible. This data element allows multiple occurrences.">
				>
				["at0007"] = <
					text = <"Additional details">
					description = <"Additional structured details about the physical assistance.">
				>
				["at0008"] = <
					text = <"Scheduled date/time">
					description = <"The intended date and/or time for provision of the physical assistance.">
					comment = <"This data element must only be used in conjunction with the 'Assistance scheduled' careflow step.">
				>
				["at0009"] = <
					text = <"Comment">
					description = <"Additional narrative about the physical assistance activity or careflow step not captured in other fields.">
				>
				["at0010"] = <
					text = <"Assistance planned">
					description = <"An intent or need to initiate the assistance process has been recognised; active careflow steps have not commenced.">
				>
				["at0011"] = <
					text = <"Assistance request sent">
					description = <"A request to initiate the assistance process has been submitted; active careflow steps have not commenced.">
				>
				["at0012"] = <
					text = <"Assistance postponed">
					description = <"The planned assistance has been temporarily stopped; active careflow steps have not commenced.">
				>
				["at0013"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0014"] = <
					text = <"Assistance cancelled">
					description = <"The planned assistance has been permanently stopped; active careflow steps have not commenced.">
				>
				["at0015"] = <
					text = <"Assistance scheduled">
					description = <"A date and/or time has been scheduled for the planned assistance; active careflow steps have not commenced.">
				>
				["at0016"] = <
					text = <"Assistance provided">
					description = <"The assistance has been provided; active careflow steps are in process.">
					comment = <"If the assistance consists of a single component, this careflow step and the 'Assistance completed' step may be recorded simultaneously. If the assistance consists of multiple components, record a separate instance of this careflow step for each component provided, and record the 'Assistance completed' step only after the final component has been delivered.">
				>
				["at0017"] = <
					text = <"Assistance suspended">
					description = <"The active assistance process has been temporarily stopped; active careflow steps have been put on hold.">
				>
				["at0018"] = <
					text = <"Assistance abandoned">
					description = <"The active assistance process has been permanently stopped; active careflow steps have been aborted.">
				>
				["at0019"] = <
					text = <"Assistance completed">
					description = <"All components of the assistance have been delivered; active careflow steps have been completed.">
					comment = <"If the assistance consists of a single component, this careflow step and the 'Assistance supply delivered' step may be recorded simultaneously.">
				>
				["at0020"] = <
					text = <"Requestor order identifier">
					description = <"The local ID assigned to the order by the 'Requestor'.">
				>
				["at0021"] = <
					text = <"Requestor">
					description = <"Details about the healthcare provider or organisation requesting the order.">
				>
				["at0022"] = <
					text = <"Receiver order identifier">
					description = <"The ID assigned to the order by the 'Receiver'.">
					comment = <"This may also be referred to as 'Filler order identifier'.">
				>
				["at0023"] = <
					text = <"Receiver">
					description = <"Details about the healthcare provider or organisation who received the request.">
				>
				["at0024"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models or formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
				["at0025"] = <
					text = <"Outcome">
					description = <"The result or consequence of the assistance provided, reflecting the individual's ability to complete the activity with the support.">
					comment = <"For example: completed successfully, partially completed, unable to complete, or ineffective. This data element must only be used in conjunction with the 'Assistance provided' or 'Assistance completed' careflow step.">
				>
			>
		>
	>
