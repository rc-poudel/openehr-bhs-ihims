archetype (adl_version=1.4; uid=9272290a-50d5-4f5e-b525-e231b5e51ad8)
	openEHR-EHR-CLUSTER.lesions.v0

concept
	[at0000]

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["date"] = <"2025-07-21">
		["name"] = <"Keisha Barwise">
		["organisation"] = <"openEHR Foundation">
		["email"] = <"keisha.barwise@openehr.org">
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Nicola Ewen Hall, openEHR Fellow 2025, Jamaica","Khin Aung, Thrive Consultants, Jamaica","Heather Leslie, openEHR Foundation, Chief Clinical Information Officer">
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the presence and summary characteristics of one or more lesions observed during physical examination.">
			keywords = <"Lesion, mass, ulcer, nodule, swelling, inspection, polyp.", ...>
			copyright = <"Â© openEHR Foundation">
			use = <"Use to record the observed findings during the physical examination of one or more lesions.

This archetype has been specifically designed to be used within CLUSTER archetypes, where clinically appropriate. 

Use to incorporate the narrative descriptions of clinical findings within existing or legacy clinical systems into an archetyped format, using the 'Clinical Description' data element.">
			misuse = <"Not to be used for recording the clinical history - use specific OBSERVATION and CLUSTER archetypes. For example OBSERVATION.story and CLUSTER.symptom_sign.

Not to be used for recording the results of an imaging examination - use the CLUSTER.imaging_exam family of archetypes for this purpose.">
		>
	>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["current_contact"] = <"Heather Leslie, Atomica Informatics, Australia<heather.leslie@atomicainformatics.com>">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"6F7ED37C3CEA002162F5D699ABB9C47B">
		["build_uid"] = <"12560271-d184-46a9-ac0d-680229465a41">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000] matches {    -- Lesions
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {    -- Lesion type
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {    -- Presence
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0003,    -- Present
							at0004,    -- Absent
							at0005]    -- Indeterminate
						}
					}
				}
			}
			ELEMENT[at0006] occurrences matches {1..*} matches {    -- Number of lesions
				value matches {
					DV_COUNT matches {*}
				}
			}
			ELEMENT[at0007] occurrences matches {0..1} matches {    -- Clinical descriptions
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0009] occurrences matches {0..*} matches {    -- Additional details
				include
					archetype_id/value matches {/.*/}
			}
			allow_archetype CLUSTER[at0008] occurrences matches {0..*} matches {    -- Per lesion
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.exam_lesion(-[a-zA-Z0-9_]+)*\.v0/}
			}
			ELEMENT[at0010] occurrences matches {0..1} matches {    -- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Lesions">
					description = <"Findings observed during the physical examination of lesions.">
				>
				["at0001"] = <
					text = <"Lesion type">
					description = <"A short descriptive name or identifier for the lesion or mass.">
				>
				["at0002"] = <
					text = <"Presence">
					description = <"Assertion about the observed presence or absence of a lesion or mass during the examination.">
				>
				["at0003"] = <
					text = <"Present">
					description = <"">
				>
				["at0004"] = <
					text = <"Absent">
					description = <"">
				>
				["at0005"] = <
					text = <"Indeterminate">
					description = <"">
				>
				["at0006"] = <
					text = <"Number of lesions">
					description = <"The total count of distinct lesions or masses identified.">
				>
				["at0007"] = <
					text = <"Clinical descriptions">
					description = <"Narrative description of the overall findings observed during the examination per lesion type.">
				>
				["at0008"] = <
					text = <"Per lesion">
					description = <"Specific features or attributes that apply to this individual lesion or mass.">
				>
				["at0009"] = <
					text = <"Additional details">
					description = <"Relevant clinical observations about the lesion type or masses that are not otherwise described.">
				>
				["at0010"] = <
					text = <"Comment">
					description = <"Additional narrative about the findings on examination of the lesion, not captured in other fields.">
				>
			>
		>
	>
